// -----------------------------------------
// Runtime model
// Notice that we can hold at most one permission
// to *fields*, but this does not apply to *predicates*
predicate time_credit() // represents one abstract unit of time

// models spending an abstract unit of time 
// needs to be called at the beginning of every method
// and loop iteration
method consume_time_credit() // 
    requires acc(time_credit(), 1/1)
// -----------------------------------------


// -----------------------------------------
// Mathematical definition of n^e
function math_pow(n: Int, e: Int): Int
    requires 0 <= e
{
    e == 0 ? 1 : n * math_pow(n, e - 1)
}
// -----------------------------------------

// -----------------------------------------
// You can use this lemma without a proof
method lemma_pow(b: Int, y: Int) 
    requires 0 <= y
    requires y % 2 == 0 // y is even
    ensures math_pow(b, y) == math_pow(b * b, y / 2)

method lemma_pow_odd(b: Int, y: Int, res: Int, target: Int)
    requires 0 < y
    requires y % 2 == 1 // y is odd
    requires res * math_pow(b, y) == target
    ensures (res * b) * math_pow(b * b, y / 2) == target
{
    lemma_pow(b, y - 1)
}
// -----------------------------------------

// -----------------------------------------
function iterations_needed(e: Int): Int
    requires 0 < e
    ensures result >= 1
{
    e == 1 ? 1 : 1 + iterations_needed(e / 2)
}
// -----------------------------------------


// -----------------------------------------
// Task 2: Prove a runtime bound for the following recursive method
// Your bound should be tight, i.e. use as few time credits
// as possible. Furthermore, try to prove that it is not possible
// to use fewer time credits.
// Do not modify the given production code. However, feel free to 
// introduce additional functions, lemma methods or ghost code.
// Furthermore, make sure that the functional contract remains intact.
method fast_pow(n: Int, e: Int)
    returns (res: Int)
    requires 0 < e
    requires acc(time_credit(), (1 + iterations_needed(e))/1)
    ensures res == math_pow(n, e)
{
    consume_time_credit() // we must spend a credit for every call

    var b: Int := n
    var y: Int := e 
    res := 1

    while (y > 0)
        invariant 0 <= y
        invariant res * math_pow(b, y) == math_pow(n, e)
        invariant y > 0 ==> acc(time_credit(), iterations_needed(y)/1)
    {
        consume_time_credit() // we must spend a credit for every loop iteration
        
        if (y % 2 == 1) {
            lemma_pow_odd(b, y, res, math_pow(n, e))
            res := res * b
        } else {
            lemma_pow(b, y)
        }
        
        y := y / 2
        b := b * b
    }
}
// -----------------------------------------
